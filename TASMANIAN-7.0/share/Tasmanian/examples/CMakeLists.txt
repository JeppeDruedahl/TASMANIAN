cmake_minimum_required(VERSION 3.10)
cmake_policy(VERSION 3.10)
project(Tasmanian_Examples)

# if given the Tasmanian install path, find_package() can be used to include the correct version of Tasmanian with all targets
find_package(Tasmanian 7.0.0 PATHS "C:/TASMANIAN-7.0"
             REQUIRED  STATIC SHARED OPENMP)

# Alternatively, a project can skip PATHS, but "C:/TASMANIAN-7.0/lib/" must be included in CMAKE_PREFIX_PATH
# this can be done from the command line with option -D CMAKE_PREFIX_PATH="C:/TASMANIAN-7.0/lib/" then
# find_package(Tasmanian 7.0.0)

# Tasmanian library targets always end on either _shared or _static to denote the type of library
# which can be helpful to select a specific type of library (if desired)
# Either shared or static libraries are always available, in some cases both may be present

# The find_package(Tasmanian) command will also generate interface targets without _shared/_static appendix
# Tasmanian_libsparsegrid, Tasmanian_libdream, and Tasmanian_libfortran90 TARGETs will always point to a valid library
# if both _shared and _static libraries are present, the targets above will point to the _static ones
# Tasmanian_libsparsegrid, Tasmanian_libdream, and Tasmanian_libfortran90
# are just a convinient way to tell cmake to link to whatever is available
# without having to explicitly check for _static or _shared targets

add_executable(example_sparse_grids  example_sparse_grids_01.cpp
                                     example_sparse_grids_02.cpp
                                     example_sparse_grids_03.cpp
                                     example_sparse_grids_04.cpp
                                     example_sparse_grids_05.cpp
                                     example_sparse_grids_06.cpp
                                     example_sparse_grids_07.cpp
                                     example_sparse_grids_08.cpp
                                     example_sparse_grids_09.cpp
                                     example_sparse_grids_10.cpp
                                     example_sparse_grids.cpp)

add_executable(example_dream         example_dream_01.cpp
                                     example_dream_02.cpp
                                     example_dream_03.cpp
                                     example_dream_04.cpp
                                     example_dream_05.cpp
                                     example_dream.cpp)

target_link_libraries(example_sparse_grids  Tasmanian::Tasmanian)
target_link_libraries(example_dream         Tasmanian::Tasmanian)

# if target Tasmanian::Fortran is present,
# then Tasmanian was build with Fortran support
if (TARGET Tasmanian::Fortran)
    enable_language(Fortran)
    add_executable(example_sparse_grids_f90  example_sparse_grids.f90)
    # Tasmanian::Tasmanian and Tasmanian::Fortran are equivalent even for CXX code
    target_link_libraries(example_sparse_grids_f90  Tasmanian::Tasmanian)
    set_property(TARGET example_sparse_grids_f90 PROPERTY LINKER_LANGUAGE Fortran)
endif()

add_custom_command(TARGET example_sparse_grids PRE_BUILD COMMAND Tasmanian::tasgrid -v)
